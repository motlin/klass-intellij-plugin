{
  parserClass="com.klass.intellij.parser.KlassParser"
  parserUtilClass="com.klass.intellij.parser.KlassParserUtil"
  psiPackage="com.klass.intellij.psi"
  psiImplPackage="com.klass.intellij.psi.impl"

  elementTypeHolderClass="com.klass.intellij.psi.KlassTypes"
  elementTypeClass="com.klass.intellij.psi.KlassElementType"
  psiClassPrefix="Klass"
  psiImplClassSuffix="Impl"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiImplUtilClass="com.klass.intellij.psi.impl.KlassPsiImplUtil"
  tokenTypeClass="com.klass.intellij.psi.KlassTokenType"

  mixin("klass|association|enumeration|primitiveTypeProperty|enumerationProperty|parameterizedProperty|associationEnd|parameterDeclaration|enumerationLiteral") = "com.klass.intellij.psi.impl.KlassNamedElementImpl"
  mixin("projection")="com.klass.intellij.psi.impl.AbstractKlassProjectionNode"
  mixin("projectionAssociationEndNode|projectionParameterizedPropertyNode")="com.klass.intellij.psi.impl.AbstractKlassInnerProjectionNode"
  implements("klass|association|enumeration|projection|member|primitiveTypeProperty|enumerationProperty|parameterizedProperty|associationEnd|parameterDeclaration|enumerationLiteral") = "com.klass.intellij.psi.KlassNamedElement"
  implements("projection|projectionInnerNode")="com.klass.intellij.psi.KlassTypedElement"
  implements("expressionMemberName")="com.klass.intellij.psi.AbstractKlassExpressionMemberName"

  tokens = [
    LBRACE='{'
    RBRACE='}'
    LBRACKET='['
    RBRACKET=']'
    LPAREN='('
    RPAREN=')'
    COLON=':'
    DOT='.'
    DOTDOT='..'
    SLASH='/'
    TICK='`'
    COMMA=','
    ANDAND='&&'
    OROR='||'
    EQEQ='=='
    NE='!='
    LT='<'
    GT='>'
    LE='<='
    GE='>='
    PLUS='+'
    MINUS='-'
    PERCENT='%'
    EXCL='!'
    QUESTION_MARK='?'
    EQ='='
    AND='&'
    SEMICOLON=';'
    IN_OPERATOR='in'
    CONTAINS_OPERATOR='contains'
    STARTS_WITH_OPERATOR='startsWith'
    ENDS_WITH_OPERATOR='endsWith'
    PACKAGE_KEYWORD='package'
    CLASS_KEYWORD='class'
    ASSOCIATION_KEYWORD='association'
    ENUMERATION_KEYWORD='enumeration'
    PROJECTION_KEYWORD='projection'
    SERVICE_KEYWORD='service'
    USER_KEYWORD='user'
    KEY_MODIFIER='key'
    PRIVATE_MODIFIER='private'
    OWNED_MODIFIER='owned'
    ID_MODIFIER='id'
    USER_ID_MODIFIER='userId'
    VALID_MODIFIER='valid'
    VALID_FROM_MODIFIER='validFrom'
    VALID_TO_MODIFIER='validTo'
    SYSTEM_MODIFIER='system'
    SYSTEM_FROM_MODIFIER='systemFrom'
    SYSTEM_TO_MODIFIER='systemTo'
    ON_KEYWORD='on'
    RELATIONSHIP_KEYWORD='relationship'
    GET_VERB='GET'
    POST_VERB='POST'
    PUT_VERB='PUT'
    PATCH_VERB='PATCH'
    DELETE_VERB='DELETE'
    CRITERIA_KEYWORD='criteria'
    VALIDATE_KEYWORD='validate'
    CONFLICT_KEYWORD='conflict'
    AUTHORIZE_KEYWORD='authorize'
    NATIVE_KEYWORD='native'
    MULTIPLICITY_KEYWORD='multiplicity'
    ORDER_BY_KEYWORD='orderBy'
    ASCENDING_KEYWORD='ascending'
    DESCENDING_KEYWORD='descending'
    ONE_KEYWORD='one'
    MANY_KEYWORD='many'
    THIS_KEYWORD='this'
    ALL_KEYWORD='all'
    EQUALS_EDGEPOINT_KEYWORD='equalsEdgePoint'

    TRUE_KEYWORD='true'
    FALSE_KEYWORD='false'
    NULL_KEYWORD='null'
  ]

  extends("klass|association|enumeration|projection|serviceGroup")=topLevelItem
  extends("primitiveTypeProperty|enumerationProperty|parameterizedProperty")=member
  extends("projectionAssociationEndNode|projectionParameterizedPropertyNode|projectionLeafNode")=projectionNode
  extends("expressionThisMember|expressionTypeMember")=expressionMemberName
  extends("criteriaGroup|criteriaAll|criteriaOperator|criteriaEdgePoint|criteriaNative")=atomicCriteria
  extends("expressionLiteral|expressionLiteralList|expressionMemberName|expressionNativeValue|expressionVariableName")=expressionValue
}

compilationUnit ::= package topLevelItem*

package ::= PACKAGE_KEYWORD packagePart ('.' packagePart)*
packagePart ::= nombre

topLevelItem ::= klass | association | enumeration | projection | serviceGroup {recoverWhile="topLevelItem_recover"}
// TODO: Recovery rule isn't working
private topLevelItem_recover ::= !('}' | CLASS_KEYWORD | USER_KEYWORD | ASSOCIATION_KEYWORD | ENUMERATION_KEYWORD | PROJECTION_KEYWORD | SERVICE_KEYWORD)

klass ::= (CLASS_KEYWORD | USER_KEYWORD) nombre serviceProjection* classModifier* versions? lBrace member* rBrace {pin="{" methods=[setName getPresentation]}
association ::= ASSOCIATION_KEYWORD nombre versions? '{' associationEnd* relationship? '}' {pin="{" methods=[setName getPresentation]}
enumeration ::= ENUMERATION_KEYWORD nombre lBrace (enumerationLiteral COMMA)* rBrace {pin="{" methods=[setName getPresentation]}
projection ::= PROJECTION_KEYWORD nombre ('(' parameterDeclaration (',' parameterDeclaration)* ')')? 'on' klassName lBrace (projectionNode ',')* rBrace {pin="{" methods=[setName getPresentation]}
// TODO: Consider moving serviceGroup inside class
serviceGroup ::= SERVICE_KEYWORD klassName lBrace urlGroup* rBrace {pin="{" methods=[getPresentation]}

projectionNode ::= projectionAssociationEndNode | projectionParameterizedPropertyNode | projectionLeafNode
projectionAssociationEndNode ::= associationEndName ':' '{' (projectionNode ',')* '}'
// TODO: This only allows passing scoped variables, but expressions (literals) should work here
projectionParameterizedPropertyNode ::= parameterizedPropertyName '(' (parameterName (',' parameterName)*)? ')' ':' '{' (projectionNode ',')* '}'
projectionLeafNode ::= memberName ':'? header?
header ::= STRING_LITERAL

urlGroup ::= url service+ {methods=[getPresentation]}
url ::= urlPart+ '/'? ('?' queryParamPart ('&' queryParamPart)*)?
urlPart ::= ('/' urlConstant) | ('/' '{' parameterDeclaration '}') {pin='/'}
urlConstant ::= nombre
queryParamPart ::= '{' parameterDeclaration '}'
service ::= verb lBrace serviceMultiplicityClause? serviceCriteriaClause? serviceAuthorizeClause? serviceValidateClause? serviceConflictClause? serviceProjectionClause serviceOrderByClause? rBrace {pin=1 methods=[getPresentation]}

// TODO: This order of criteria may not match antlr
serviceMultiplicityClause ::= MULTIPLICITY_KEYWORD ':' serviceMultiplicity SEMICOLON
serviceCriteriaClause ::= CRITERIA_KEYWORD ':' criteriaExpression SEMICOLON
serviceAuthorizeClause ::= 'authorize' ':' criteriaExpression SEMICOLON
serviceValidateClause ::= VALIDATE_KEYWORD ':' criteriaExpression SEMICOLON
serviceConflictClause ::= CONFLICT_KEYWORD ':' criteriaExpression SEMICOLON
// TODO: This only allows passing scoped variables, but expressions (literals) should work here
serviceProjectionClause ::= PROJECTION_KEYWORD ':' projectionName ('(' parameterName (',' parameterName)* ')')? SEMICOLON
private serviceOrderByClause ::= orderByClause SEMICOLON

orderByClause ::= ORDER_BY_KEYWORD ':' (orderByProperty) (',' orderByProperty)*
orderByProperty ::= THIS_KEYWORD '.' memberName orderByDirection?
orderByDirection ::= ASCENDING_KEYWORD | DESCENDING_KEYWORD
serviceMultiplicity ::= 'one' | 'many'
verb ::= 'GET' | 'POST' | 'PUT' | 'PATCH' | 'DELETE'

// Left recursion isn't well supported in Grammar-Kit
criteriaExpression ::= criteriaAnd
criteriaAnd ::= (criteriaOr '&&' criteriaOr) | criteriaOr
criteriaOr ::= (atomicCriteria '||' atomicCriteria) | atomicCriteria
atomicCriteria ::= criteriaGroup | criteriaAll | criteriaOperator | criteriaEdgePoint | criteriaNative

criteriaGroup ::= '(' criteriaExpression ')'
criteriaAll ::= ALL_KEYWORD;
// Overly generic, since clauses like 1 == 2 and 2 == $id don't make sense
criteriaOperator ::= sourceExpressionValue operator targetExpressionValue
criteriaEdgePoint ::= expressionMemberName EQUALS_EDGEPOINT_KEYWORD
criteriaNative ::= 'native' '(' nombre ')'
sourceExpressionValue ::= expressionValue
targetExpressionValue ::= expressionValue

expressionValue ::= expressionLiteral | expressionLiteralList | expressionThisMember | expressionTypeMember | expressionNativeValue | expressionVariableName
expressionLiteral ::= integerLiteralNode | floatLiteralNode | stringLiteralNode | booleanLiteral
expressionLiteralList ::= '(' expressionLiteral (',' expressionLiteral)* ')'

expressionMemberName ::= expressionThisMember | expressionTypeMember
expressionThisMember ::= THIS_KEYWORD '.' (associationEndName '.')* memberName
expressionTypeMember ::= klassName '.' (associationEndName '.')* memberName

operator ::= EQEQ | NE | LT | GT | LE | GE | 'in' | CONTAINS_OPERATOR | STARTS_WITH_OPERATOR | ENDS_WITH_OPERATOR

relationship ::= RELATIONSHIP_KEYWORD criteriaExpression

member ::= primitiveTypeProperty | enumerationProperty | parameterizedProperty
// TODO: Make semi-colon required
primitiveTypeProperty ::= nombre ':' primitiveType optionalMarker? propertyModifier* SEMICOLON {methods=[setName getPresentation]}
enumerationProperty ::= nombre ':' enumerationType optionalMarker? propertyModifier* SEMICOLON {methods=[setName getPresentation]}
parameterizedProperty ::= nombre '(' (parameterDeclaration (',' parameterDeclaration)*)? ')' ':' klassName multiplicity orderByClause? lBrace criteriaExpression rBrace {methods=[setName getPresentation]}

classModifier ::= SYSTEM_TEMPORAL_MODIFIER | VALID_TEMPORAL_MODIFIER | BITEMPORAL_MODIFIER  | VERSIONED_MODIFIER | AUDITED_MODIFIER | OPTIMISTICALLY_LOCKED_MODIFIER | TRANSIENT_MODIFIER
propertyModifier ::= KEY_MODIFIER | PRIVATE_MODIFIER | ID_MODIFIER | USER_ID_MODIFIER | VALID_MODIFIER | VALID_FROM_MODIFIER | VALID_TO_MODIFIER | SYSTEM_MODIFIER | SYSTEM_FROM_MODIFIER | SYSTEM_TO_MODIFIER
associationEndModifier ::= OWNED_MODIFIER
parameterModifier::= VERSION_MODIFIER  | ID_MODIFIER | USER_ID_MODIFIER

versions ::= VERSIONS_KEYWORD '(' klassName ')'
serviceProjection ::= serviceCategory ('(' projectionName ')')?
serviceCategory ::= READ_KEYWORD | WRITE_KEYWORD | CREATE_KEYWORD | UPDATE_KEYWORD | DELETE_KEYWORD

// TODO: packages, imports, fully qualified class names
// TODO: split property keywords and association end keywords
associationEnd ::= nombre ':' klassName multiplicity associationEndModifier* orderByClause? SEMICOLON {methods=[setName getPresentation]}

lBrace ::= '{'
rBrace ::= '}'

// Just can't be called name, because the getName method would clash
nombre ::= nombreText // TODO: Inline
nombreText ::= IDENTIFIER | PACKAGE_KEYWORD | CLASS_KEYWORD | ASSOCIATION_KEYWORD | ENUMERATION_KEYWORD | PROJECTION_KEYWORD | SERVICE_KEYWORD | USER_KEYWORD | KEY_MODIFIER | PRIVATE_MODIFIER | OWNED_MODIFIER | SYSTEM_TEMPORAL_MODIFIER | VALID_TEMPORAL_MODIFIER | BITEMPORAL_MODIFIER  | VERSIONED_MODIFIER | AUDITED_MODIFIER | OPTIMISTICALLY_LOCKED_MODIFIER | TRANSIENT_MODIFIER | VERSIONS_KEYWORD | USER_ID_MODIFIER | ID_MODIFIER | VALID_MODIFIER | VALID_FROM_MODIFIER | VALID_TO_MODIFIER | SYSTEM_MODIFIER | SYSTEM_FROM_MODIFIER | SYSTEM_TO_MODIFIER | BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD | TEMPORAL_INSTANT_KEYWORD | TEMPORAL_RANGE_KEYWORD | READ_KEYWORD | WRITE_KEYWORD | CREATE_KEYWORD | UPDATE_KEYWORD | DELETE_KEYWORD | MULTIPLICITY_KEYWORD | ORDER_BY_KEYWORD | CONTAINS_OPERATOR | STARTS_WITH_OPERATOR | ENDS_WITH_OPERATOR | IN_OPERATOR | ALL_KEYWORD | EQUALS_EDGEPOINT_KEYWORD

enumerationLiteral ::= nombre ('(' enumerationPrettyName ')')? {methods=[setName getPresentation]}

parameterDeclaration ::= nombre (':' dataTypeDeclaration)? parameterModifier* {methods=[setName]}
dataTypeDeclaration ::= (primitiveType | enumerationType) multiplicity
primitiveType ::= BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD | TEMPORAL_INSTANT_KEYWORD | TEMPORAL_RANGE_KEYWORD {methods=[getReference]}
enumerationType ::= nombre {methods=[getReference]}
enumerationPrettyName ::= STRING_LITERAL
klassName ::= nombre {methods=[getReference]}
memberName ::= nombre {methods=[getReference]}
associationEndName ::= nombre {methods=[getReference]}
parameterizedPropertyName ::= nombre {methods=[getReference]}
projectionName ::= nombre {methods=[getReference]}
expressionNativeValue ::= USER_KEYWORD {methods=[getReference]}
expressionVariableName ::= nombre {methods=[getReference]}
parameterName ::= nombre {methods=[getReference]}
optionalMarker ::= QUESTION_MARK

multiplicity ::= '[' ((lowerBound '..' upperBound) | dummyMultiplicity) ']'
dummyMultiplicity ::= lowerBound | lowerBound '..' {methods=[getReference]}

lowerBound ::= INTEGER_LITERAL
upperBound ::= INTEGER_LITERAL | ASTERISK
booleanLiteral ::= TRUE_KEYWORD | FALSE_KEYWORD | NULL_KEYWORD
integerLiteralNode ::= INTEGER_LITERAL
floatLiteralNode ::= FLOAT_LITERAL
stringLiteralNode ::= STRING_LITERAL
