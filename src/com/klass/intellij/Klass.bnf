{
  parserClass="com.klass.intellij.parser.KlassParser"
  parserUtilClass="com.klass.intellij.parser.KlassParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Klass"
  psiImplClassSuffix="Impl"
  psiPackage="com.klass.intellij.psi"
  psiImplPackage="com.klass.intellij.psi.impl"

  elementTypeHolderClass="com.klass.intellij.psi.KlassTypes"
  elementTypeClass="com.klass.intellij.psi.KlassElementType"
  tokenTypeClass="com.klass.intellij.psi.KlassTokenType"

  psiImplUtilClass="com.klass.intellij.psi.impl.KlassPsiImplUtil"
}

compilationUnit ::= topLevelItem_*

private topLevelItem_ ::= class | association

class ::= CLASS_KEYWORD className LBRACE dataTypeProperty* RBRACE {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[getName setName getNameIdentifier getPresentation]}

association ::= ASSOCIATION_KEYWORD associationName LBRACE sourceAssociationEnd targetAssociationEnd RBRACE {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[getName setName getNameIdentifier getPresentation]}

dataTypeProperty ::= dataTypePropertyName COLON dataType optionalMarker? {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[getName setName getNameIdentifier getPresentation]}
sourceAssociationEnd ::= associationEnd;
targetAssociationEnd ::= associationEnd;

// TODO: packages, imports, fully qualified class names
associationEnd ::= associationEndName? COLON associationEndType? multiplicity? {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[getName setName getNameIdentifier getPresentation]}

className ::= IDENTIFIER
associationEndType ::= IDENTIFIER {methods=[getReference]}
associationName ::= IDENTIFIER
dataTypePropertyName ::= IDENTIFIER
associationEndName ::= IDENTIFIER
dataType ::= BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD {methods=[getReference]}
optionalMarker ::= QUESTION_MARK

multiplicity ::= LBRACKET lowerBound DOTDOT upperBound RBRACKET
lowerBound ::= INTEGER_LITERAL
upperBound ::= INTEGER_LITERAL | ASTERISK
