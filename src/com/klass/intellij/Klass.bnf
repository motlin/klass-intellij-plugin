{
  parserClass="com.klass.intellij.parser.KlassParser"
  parserUtilClass="com.klass.intellij.parser.KlassParserUtil"
  psiPackage="com.klass.intellij.psi"
  psiImplPackage="com.klass.intellij.psi.impl"

  elementTypeHolderClass="com.klass.intellij.psi.KlassTypes"
  elementTypeClass="com.klass.intellij.psi.KlassElementType"
  psiClassPrefix="Klass"
  psiImplClassSuffix="Impl"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiImplUtilClass="com.klass.intellij.psi.impl.KlassPsiImplUtil"
  tokenTypeClass="com.klass.intellij.psi.KlassTokenType"

  mixin("klass|association|enumeration|dataTypeProperty|enumerationProperty|associationEnd|pathParameter") = "com.klass.intellij.psi.impl.KlassNamedElementImpl"
  mixin("projection")="com.klass.intellij.psi.impl.AbstractKlassProjectionNode"
  mixin("projectionInnerNode")="com.klass.intellij.psi.impl.AbstractKlassInnerProjectionNode"
  implements("klass|association|enumeration|projection|property|dataTypeProperty|enumerationProperty|associationEnd|pathParameter") = "com.klass.intellij.psi.KlassNamedElement"
  implements("projection|projectionInnerNode")="com.klass.intellij.psi.KlassTypedElement"

  tokens = [
    LBRACE='{'
    RBRACE='}'
    LBRACKET='['
    RBRACKET=']'
    LPAREN='('
    RPAREN=')'
    COLON=':'
    DOT='.'
    DOTDOT='..'
    SLASH='/'
    TICK='`'
    ANDAND='&&'
    OROR='||'
    EQEQ='=='
    NE='!='
    LT='<'
    GT='>'
    LE='<='
    GE='>='
    PLUS='+'
    MINUS='-'
    PERCENT='%'
    EXCL='!'
    CLASS_KEYWORD='class'
    ASSOCIATION_KEYWORD='association'
    ENUMERATION_KEYWORD='enumeration'
    PROJECTION_KEYWORD='projection'
    SERVICE_KEYWORD='service'
    KEY_KEYWORD='key'
    GET_VERB='GET'
    POST_VERB='POST'
    PUT_VERB='PUT'
    PATCH_VERB='PATCH'
    DELETE_VERB='DELETE'
    CRITERIA_KEYWORD='criteria'
    MULTIPLICITY_KEYWORD='multiplicity'
    ONE_KEYWORD='one'
    MANY_KEYWORD='many'
    THIS_KEYWORD='this'
  ]

  extends("klass|association|enumeration|projection|serviceGroup")=topLevelItem
  extends("dataTypeProperty|enumerationProperty")=property
  extends("projectionInnerNode|projectionLeafNode")=projectionNode
  extends("criteriaAnd|criteriaOr|criteriaMethod|criteriaOperator")=criteriaExpression
}

compilationUnit ::= topLevelItem*

topLevelItem ::= klass | association | enumeration | projection | serviceGroup {recoverWhile="topLevelItem_recover"}
private topLevelItem_recover ::= !('}' | 'class' | 'association' | 'enumeration' | 'projection' | 'service')

klass ::= 'class' klassKeyword* nombre lBrace property* rBrace {pin="{" methods=[setName getPresentation]}
association ::= 'association' nombre '{' associationEnd* '}' {pin="{" methods=[setName getPresentation]}
enumeration ::= 'enumeration' nombre lBrace (enumerationLiteral COMMA)* rBrace {pin="{" methods=[setName getPresentation]}
projection ::= 'projection' nombre '(' klassName ')' lBrace (projectionNode ',')* rBrace {pin="{" methods=[setName getPresentation]}
serviceGroup ::= 'service' klassName lBrace urlGroup* rBrace {pin="{" methods=[getPresentation]}

projectionNode ::= projectionInnerNode | projectionLeafNode
projectionInnerNode ::= associationEndName ':' '{' (projectionNode ',')* '}'
projectionLeafNode ::= propertyName ':'? header?
header ::= STRING_LITERAL

urlGroup ::= url service+ {methods=[getPresentation]}
url ::= urlPart+ '/'?
urlPart ::= ('/' urlConstant) | ('/' '{' pathParameter '}') {pin='/'}
urlConstant ::= nombre
pathParameter ::= nombre {methods=[setName getPresentation]}
service ::= verb lBrace serviceMultiplicityClause serviceCriteriaClause? serviceProjectionClause rBrace {pin=1 methods=[getPresentation]}

serviceMultiplicityClause ::= 'multiplicity' ':' serviceMultiplicity
serviceCriteriaClause ::= 'criteria' ':' serviceCriteria
serviceProjectionClause ::= 'projection' ':' serviceProjection

serviceMultiplicity ::= 'one' | 'many'
serviceCriteria ::= criteriaExpression
serviceProjection::= projectionName
verb ::= 'GET' | 'POST' | 'PUT' | 'PATCH' | 'DELETE'

criteriaExpression ::= criteriaAnd | criteriaOr | criteriaMethod | criteriaOperator
criteriaAnd ::= criteriaExpression '&&' criteriaExpression
criteriaOr ::= criteriaExpression '||' criteriaExpression
criteriaMethod ::= 'this' '.' propertyName '.' methodName '(' (expressionValue (',' expressionValue)*)? ')'
criteriaOperator ::= 'this' '.' propertyName operator expressionValue
expressionValue ::= expressionLiteral | expressionVariableName
expressionLiteral ::= INTEGER_LITERAL | LONG_LITERAL | DOUBLE_LITERAL | FLOAT_LITERAL | STRING_LITERAL
operator ::= EQEQ | NE | LT | GT | LE | GE

property ::= dataTypeProperty | enumerationProperty
dataTypeProperty ::= propertyKeyword* nombre ':' dataType optionalMarker? {methods=[setName getPresentation]}
enumerationProperty ::= propertyKeyword* nombre ':' enumerationType optionalMarker? {methods=[setName getPresentation]}

klassKeyword ::= SYSTEM_TEMPORAL_KEYWORD | VALID_TEMPORAL_KEYWORD | BITEMPORAL_KEYWORD | VERSIONED_KEYWORD
propertyKeyword ::= 'key'

// TODO: packages, imports, fully qualified class names
associationEnd ::= nombre ':' klassName? multiplicity? {methods=[setName getPresentation]}

lBrace ::= '{'
rBrace ::= '}'

// Just can't be called name, because the getName method would clash
nombre ::= nombreText | '`' nombreText '`'
nombreText ::= IDENTIFIER | 'class' | 'association' | 'enumeration' | 'projection' | 'service' | 'key' | SYSTEM_TEMPORAL_KEYWORD | VALID_TEMPORAL_KEYWORD | BITEMPORAL_KEYWORD | VERSIONED_KEYWORD | ID_KEYWORD | BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD
enumerationLiteral ::= nombre

dataType ::= ID_KEYWORD | BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD {methods=[getReference]}
enumerationType ::= nombre {methods=[getReference]}
klassName ::= nombre {methods=[getReference]}
propertyName ::= nombre {methods=[getReference]}
associationEndName ::= nombre {methods=[getReference]}
projectionName ::= nombre {methods=[getReference]}
methodName ::= nombre
expressionVariableName ::= nombre {methods=[getReference]}
optionalMarker ::= QUESTION_MARK

multiplicity ::= '[' ((lowerBound '..' upperBound) | dummyMultiplicity) ']'
dummyMultiplicity ::= lowerBound | lowerBound '..' {methods=[getReference]}

lowerBound ::= INTEGER_LITERAL
upperBound ::= INTEGER_LITERAL | ASTERISK
