{
  parserClass="com.klass.intellij.parser.KlassParser"
  parserUtilClass="com.klass.intellij.parser.KlassParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Klass"
  psiImplClassSuffix="Impl"
  psiPackage="com.klass.intellij.psi"
  psiImplPackage="com.klass.intellij.psi.impl"

  elementTypeHolderClass="com.klass.intellij.psi.KlassTypes"
  elementTypeClass="com.klass.intellij.psi.KlassElementType"
  tokenTypeClass="com.klass.intellij.psi.KlassTokenType"

  psiImplUtilClass="com.klass.intellij.psi.impl.KlassPsiImplUtil"
}

compilationUnit ::= topLevelItem_*

private topLevelItem_ ::= class | association | enumeration

class ::= CLASS_KEYWORD nombre LBRACE property* RBRACE {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

association ::= ASSOCIATION_KEYWORD nombre LBRACE sourceAssociationEnd targetAssociationEnd RBRACE {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

enumeration ::= ENUMERATION_KEYWORD nombre LBRACE (enumerationLiteral COMMA)* RBRACE {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

property ::= dataTypeProperty | enumerationProperty

dataTypeProperty ::= nombre COLON dataType optionalMarker? {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

enumerationProperty ::= nombre COLON enumerationType optionalMarker? {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

sourceAssociationEnd ::= associationEnd;
targetAssociationEnd ::= associationEnd;

// TODO: packages, imports, fully qualified class names
associationEnd ::= nombre? COLON associationEndType? multiplicity? {mixin="com.klass.intellij.psi.impl.KlassNamedElementImpl" implements="com.klass.intellij.psi.KlassNamedElement" methods=[setName getPresentation]}

// Just can't be called name, because the getName method would clash
nombre ::= IDENTIFIER
associationEndType ::= IDENTIFIER {methods=[getReference]}
enumerationLiteral ::= IDENTIFIER

dataType ::= BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD {methods=[getReference]}
enumerationType ::= IDENTIFIER {methods=[getReference]}
optionalMarker ::= QUESTION_MARK

multiplicity ::= LBRACKET lowerBound DOTDOT upperBound RBRACKET
lowerBound ::= INTEGER_LITERAL
upperBound ::= INTEGER_LITERAL | ASTERISK
