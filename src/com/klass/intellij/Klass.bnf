{
  parserClass="com.klass.intellij.parser.KlassParser"
  parserUtilClass="com.klass.intellij.parser.KlassParserUtil"
  psiPackage="com.klass.intellij.psi"
  psiImplPackage="com.klass.intellij.psi.impl"

  elementTypeHolderClass="com.klass.intellij.psi.KlassTypes"
  elementTypeClass="com.klass.intellij.psi.KlassElementType"
  psiClassPrefix="Klass"
  psiImplClassSuffix="Impl"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiImplUtilClass="com.klass.intellij.psi.impl.KlassPsiImplUtil"
  tokenTypeClass="com.klass.intellij.psi.KlassTokenType"

  mixin("klass|association|enumeration|dataTypeProperty|enumerationProperty|associationEnd") = "com.klass.intellij.psi.impl.KlassNamedElementImpl"
  implements("klass|association|enumeration|dataTypeProperty|enumerationProperty|associationEnd") = "com.klass.intellij.psi.KlassNamedElement"

  tokens = [
    LBRACE='{'
    RBRACE='}'
    LBRACKET='['
    RBRACKET=']'
    COLON=':'
    DOTDOT='..'
    CLASS_KEYWORD='class'
    ASSOCIATION_KEYWORD='association'
    ENUMERATION_KEYWORD='enumeration'
  ]

  extends("klass|association|enumeration")=topLevelItem
  extends("dataTypeProperty|enumerationProperty")=property
}

compilationUnit ::= topLevelItem*

topLevelItem ::= klass | association | enumeration {recoverWhile="topLevelItem_recover"}
private topLevelItem_recover ::= !('}' | 'class' | 'association' | 'enumeration')

klass ::= 'class' nombre '{' property* '}' {pin="{" methods=[setName getPresentation]}

association ::= 'association' nombre '{' associationEnd* '}' {pin="{" methods=[setName getPresentation]}

enumeration ::= 'enumeration' nombre '{' (enumerationLiteral COMMA)* '}' {pin="{" methods=[setName getPresentation]}

property ::= dataTypeProperty | enumerationProperty

dataTypeProperty ::= nombre ':' dataType optionalMarker? {methods=[setName getPresentation]}

enumerationProperty ::= nombre ':' enumerationType optionalMarker? {methods=[setName getPresentation]}

// TODO: packages, imports, fully qualified class names
associationEnd ::= nombre ':' associationEndType? multiplicity? {methods=[setName getPresentation]}

// Just can't be called name, because the getName method would clash
nombre ::= IDENTIFIER
associationEndType ::= IDENTIFIER {methods=[getReference]}
enumerationLiteral ::= IDENTIFIER

dataType ::= BOOLEAN_KEYWORD | INTEGER_KEYWORD | LONG_KEYWORD | DOUBLE_KEYWORD | FLOAT_KEYWORD | STRING_KEYWORD | INSTANT_KEYWORD | LOCAL_DATE_KEYWORD {methods=[getReference]}
enumerationType ::= IDENTIFIER {methods=[getReference]}
optionalMarker ::= QUESTION_MARK

multiplicity ::= '[' lowerBound '..' upperBound ']'
lowerBound ::= INTEGER_LITERAL
upperBound ::= INTEGER_LITERAL | ASTERISK
